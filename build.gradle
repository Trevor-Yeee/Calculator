plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
}

group = 'com.houari'
version = '1.0-SNAPSHOT'
description = 'Calculator'
java.sourceCompatibility = JavaVersion.VERSION_1_8

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

tasks.test {
    useJUnitPlatform() // Ensure JUnit Platform is used
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
    finalizedBy(tasks.jacocoTestReport)
}

tasks.jar {
    manifest {
        attributes 'Main-Class': 'com.houarizegai.calculator.App'
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    archiveBaseName.set('calculator-app')
    destinationDirectory.set(file('build/libs'))
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.jacocoTestReport {
    dependsOn(tasks.test)
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
}
